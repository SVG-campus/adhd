# Copyright 2022 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

load("//dist:cras_rules_cc.bzl", "cras_shared_library")

cc_library(
    name = "cras_client",
    srcs = [
        "cras_client.c",
        "cras_helpers.c",
    ],
    hdrs = ["cras_client.h"],
    includes = ["."],
    linkopts = [
        "-lm",
        "-lrt",
    ],
    visibility = [
        "//src/alsa_plugin:__pkg__",
        "//src/benchmark:__pkg__",
        "//src/tools:__pkg__",
    ],
    deps = [
        "//src/common:cras_config",
        "//src/common:cras_file_wait",
        "//src/common:cras_messages",
        "//src/common:cras_observer_ops",
        "//src/common:cras_selinux_helper",
        "//src/common:cras_shm",
        "//src/common:cras_string",
        "//src/common:cras_types",
        "//src/common:cras_util",
        "//src/common:cras_version",
        "//src/common:utlist",
        "@pkg_config//:alsa",
    ],
)

cras_shared_library(
    name = "cras",  # libcras.so
    roots = [":cras_client"],
    static_deps = [
        "@//:__subpackages__",
        # Despite that we list external deps in static_deps, they are linked
        # dynamically because pkg_config does not provide object files.
        "@pkg_config//:alsa",
    ] + select({
        "//:selinux_build": ["@libselinux//:libselinux"],
        "//conditions:default": [],
    }),
    visibility = [
        "//dist:__pkg__",
        "//src/alsa_plugin:__pkg__",
    ],
)

filegroup(
    name = "public_headers",
    srcs = [
        "cras_client.h",
        "cras_helpers.h",
    ],
    visibility = ["//dist:__pkg__"],
)

# Allow tests to not specify all headers.
cc_library(
    name = "all_headers",
    testonly = True,
    hdrs = glob(include = ["*.h"]),
    includes = ["."],
    textual_hdrs = glob(include = ["*.c"]),  # Allow including sources
    visibility = [
        "//src/tests:__pkg__",
    ],
)

# Allow tests to use sources directly
exports_files(
    glob(include = [
        "*.c",
    ]),
    visibility = [
        "//src/tests:__pkg__",
    ],
)
