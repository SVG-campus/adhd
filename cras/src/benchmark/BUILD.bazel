# Copyright 2021 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

cc_binary(
    name = "cras_bench",
    visibility = ["//dist:__pkg__"],
    deps = [
        ":benchmark_util",
        ":default_benchmarks",
        "@com_github_google_benchmark//:benchmark_main",
    ] + select({
        "//:apm_build": [":apm_benchmark"],
        "//conditions:default": [],
    }) + select({
        "//:hw_dependent_build": ["alsa_benchmark"],
        "//conditions:default": [],
    }) + select({
        "//:ml_build": ["am_benchmark"],
        "//conditions:default": [],
    }),
)

cc_library(
    name = "default_benchmarks",
    srcs = [
        "dsp_benchmark.cc",
        "mixer_ops_benchmark.cc",
    ],
    deps = [
        ":benchmark_util",
        "//src/dsp:drc",
        "//src/dsp:dsp_util",
        "//src/dsp:eq2",
        "//src/server:cras_mix",
        "@com_github_google_benchmark//:benchmark",
    ],
    alwayslink = True,
)

cc_library(
    name = "am_benchmark",
    srcs = ["am_benchmark.cc"],
    target_compatible_with = select({
        "//:ml_build": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    deps = [
        ":benchmark_util",
        "//src/dsp:am",
        "@com_github_google_benchmark//:benchmark",
    ],
    alwayslink = True,
)

cc_library(
    name = "apm_benchmark",
    srcs = [
        "apm_benchmark.cc",
    ],
    linkopts = [
        "-lwebrtc_apm",
    ],
    target_compatible_with = select({
        "//:apm_build": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    deps = [
        ":benchmark_util",
        "//src/dsp:dsp_util",
        "@com_github_google_benchmark//:benchmark",
    ],
    alwayslink = True,
)

cc_library(
    name = "alsa_benchmark",
    srcs = [
        "alsa_benchmark.cc",
    ],
    linkopts = [
        "-lasound",
        "-lcros_config",
    ],
    target_compatible_with = select({
        "//:hw_dependent_build": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    deps = [
        ":benchmark_util",
        "//src/libcras:cras_client",
        "//src/server:cras_alsa_helpers",
        "//src/server:cras_alsa_ucm",
        "//src/server:cras_mix",
        "@com_github_google_benchmark//:benchmark",
    ] + select({
        # Guard pkg_config behind hw_dependent_build so bazel does not attempt
        # to run pkg-config in the analysis phase for platforms missing the
        # dependencies.
        "//:hw_dependent_build": [
            "@libchrome",
            "@libcros_config",
        ],
        "//conditions:default": [],
    }),
    alwayslink = True,
)

cc_library(
    name = "benchmark_util",
    srcs = ["benchmark_util.cc"],
    hdrs = ["benchmark_util.h"],
)
