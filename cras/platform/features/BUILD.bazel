# Copyright 2023 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

load("//:utils.bzl", "require_config")
load("//cras/platform:defs.bzl", "COPTS")

cc_library(
    name = "features",
    srcs = [
        "features_impl.c",
        "features_impl.h",
        "features.inc",
        "override.h",
    ] + select({
        "//:featured_build": [
            "backend_featured.hh",
            "backend_featured.cc",
        ],
        "//conditions:default": ["backend_stub.c"],
    }),
    hdrs = ["features.h"],
    copts = COPTS,
    visibility = [
        "//cras/src/server:__pkg__",
        "//cras/src/tests:__pkg__",
    ],
    deps = ["//cras/base"] + select({
        "//:featured_build": [
            "@pkg_config//libchrome",
            "@pkg_config//libfeatures",
            "//cras/server:main_message",
        ],
        "//conditions:default": [],
    }),
)

cc_library(
    name = "override",
    hdrs = ["override.h"],
    copts = COPTS,
    visibility = [
        "//cras/src/server:__pkg__",
        "//cras/src/tests:__pkg__",
    ],
    deps = [":features"],
)

cc_test(
    name = "features_test",
    srcs = [
        "features_impl.h",
        "features_test.cc",
    ],
    copts = COPTS,
    deps = [
        ":features",
        "@pkg_config//gtest",
        "@pkg_config//gtest_main",
    ],
)

cc_test(
    name = "backend_featured_test",
    srcs = [
        "backend_featured.hh",
        "backend_featured_test.cc",
    ],
    copts = COPTS,
    linkopts = ["-lfake_platform_features"],
    target_compatible_with = require_config("//:featured_build"),
    deps = [
        ":features",
        "@pkg_config//gtest",
        "@pkg_config//gtest_main",
    ],
)
